<?xml version="1.0" encoding="utf-8"?>
<!--

Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-->


<!--
	This script should be used to create an Apache Flex SDK that has the
    directory structure that the Adobe Flash Player IDE expects.

	The Adobe AIR SDK and the Adobe Flash Player playerglobal.swc are integrated
    into the directory structure.  The paths in the framework configuration files are 
    modified to reflect this.  The AIR_HOME and PLAYERGLOBAL_HOME environment 
    variables are not required because the locations of these pieces are known.

	Usage: makeApacheFlexForFlashBuilder [sdk directory]

	@author OmPrakash Muppirala (bigosmallm@gmail.com)

-->

<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   width="700" height="500"
					   invoke="windowedapplication1_invokeHandler(event)" showStatusBar="false">
<fx:Script>
	<![CDATA[
		
		import mx.collections.ArrayCollection;
		
		import org.as3commons.zip.Zip;
		import org.as3commons.zip.ZipEvent;
		import org.as3commons.zip.ZipFile;
			
		/**
		 * Edit these constants if you would like to download from alternative locations.
		 * 
		 * 
		 * Apache Flex binary distribution
		 */
		private const APACHE_FLEX_BIN_DISTRO_FILE:String = "apache-flex-sdk-4.8.0-incubating-bin.zip";
		private const APACHE_FLEX_BIN_DISTRO_URL:String = "http://people.apache.org/~cframpton/ApacheFlexRC/current/" + APACHE_FLEX_BIN_DISTRO_FILE;
			
		/**
		 * 
		 * Adobe AIR SDK Version 3.1
		 * 
		 */ 
		private const ADOBE_AIR_SDK_WIN_FILE:String = "AdobeAIRSDK.zip"
		private const ADOBE_AIR_SDK_WIN_URL:String = "http://airdownload.adobe.com/air/win/download/3.1/" + ADOBE_AIR_SDK_WIN_FILE;

		/**
		 * 
		 * Adobe Flash Player Version 11.1
		 * 
		 */
		private const ADOBE_FB_GLOBALPLAYER_SWC_URL:String = "http://fpdownload.macromedia.com/get/flashplayer/updaters/11/playerglobal11_1.swc"
			
		private const APP_NAME:String = "MakeApacheFlexForFlashBuilder";
			
		private var _args:Array;
		private var _flexHome:String;
		private var _flexTemp:String;
		private var _flexHomeDir:File;
		private var _flexTempDir:File;
		private var _apacheFlexSDKZipFile:File;
		private var _adobeAIRSDKWinZipFile:File;
		private var _fbGlobalPlayerDir:File;
		
		protected function log(text:String):void{
			_logTxtArea.appendText("\n" + text);
			_logTxtArea.validateNow();
		}
		
		protected function windowedapplication1_invokeHandler(event:InvokeEvent):void
		{
			_args = event.arguments;
			main();
		}
		
		/**
		 * 
		 * Start off processing the installation.  
		 * First create the flex sdk directory and the temp dirs
		 * 
		 */
		protected function main():void{
			var isValidInvocaton:Boolean = validateArguments();
			if(isValidInvocaton){
				log(APP_NAME + " invoked with the following arguments: " + _flexHome);
				createDirectories();
				downloadApacheFlexSDK();
			}
			else{
				log("Aborting installation.");
			}
		}
		
		/**
		 * If there is no argument, show error message and exit
		 * If there are multiple arguments, its most probably a directory path with spaces.  Concatenate all of them with spaces in between
		 */
		protected function validateArguments():Boolean{
			if(_args.length == 0){
				log("Usage: " + APP_NAME + " [directory for Apache Flex SDK for Adobe Flash Builder]");
				return false;
			}
			if(_args.length > 1){
				_flexHome = _args.join(" "); 
				return true;
			}
			if(_args.length == 1){
				_flexHome = _args[0];
				return true;
			}
			return false;
		}
		
		protected function createDirectories():void{
			log("Creating flex home");
			_flexHomeDir = createFolder(_flexHome);
			log("Creating temp dir");
			_flexTemp = _flexHome+"\\temp";
			_flexTempDir = createFolder(_flexTemp);
		}
		
		protected function downloadApacheFlexSDK():void{
			log("Downloading Apache Flex SDK from: " + APACHE_FLEX_BIN_DISTRO_URL);
			download(APACHE_FLEX_BIN_DISTRO_URL, handleApacheFlexSDKDownload);
		}
		
		protected function handleApacheFlexSDKDownload(event:Event):void{
			_apacheFlexSDKZipFile = File.userDirectory.resolvePath(_flexTemp + "\\" + APACHE_FLEX_BIN_DISTRO_FILE); 
			var fs:FileStream = new FileStream(); 
			fs.open(_apacheFlexSDKZipFile, FileMode.WRITE); 
			fs.writeBytes(event.target.data); 
			fs.close();
			
			unzipApacheFlexSDK();
		}
		
		protected function unzipApacheFlexSDK():void{
			log("Unzipping: " + _apacheFlexSDKZipFile.nativePath);
			unzip(_apacheFlexSDKZipFile,handleApacheFlexSDKZipFileUnzipComplete);
		}
		
		protected function handleApacheFlexSDKZipFileUnzipComplete(event:Event):void{
			log("Finished unzipping: " + _apacheFlexSDKZipFile.nativePath);
			
			downloadAIRRuntimeKitForWindows();
		}
		
		protected function downloadAIRRuntimeKitForWindows():void{
			log("Downloading Adobe AIR Runtime Kit for Windows  from: " + ADOBE_AIR_SDK_WIN_URL);
			download(ADOBE_AIR_SDK_WIN_URL, handleAIRSDKDownload);
		}
		
		protected function handleAIRSDKDownload(event:Event):void{
			_adobeAIRSDKWinZipFile = File.userDirectory.resolvePath(_flexTemp + "\\" + ADOBE_AIR_SDK_WIN_FILE);
			var fs:FileStream = new FileStream();
			fs.open(_adobeAIRSDKWinZipFile, FileMode.WRITE);
			fs.writeBytes(event.target.data); 
			fs.close();
			
			unzipAdobeAIRSDKWin();
		}
		
		protected function unzipAdobeAIRSDKWin():void{
			log("Unzipping: " + _adobeAIRSDKWinZipFile.nativePath);
			unzip(_adobeAIRSDKWinZipFile,handleAdobeAIRSDKWinZipFileUnzipComplete);
		}
		
		protected function handleAdobeAIRSDKWinZipFileUnzipComplete(event:Event):void{
			log("Finished unzipping: " + _adobeAIRSDKWinZipFile.nativePath);
			downloadPlayerGlobalSWC();
		}
		
		protected function downloadPlayerGlobalSWC():void{
			log("Downloading Adobe Flash Player playerglobal.swc from: " + ADOBE_FB_GLOBALPLAYER_SWC_URL);
			download(ADOBE_FB_GLOBALPLAYER_SWC_URL, handlePlayerGlobalDownload);
		}
		
		protected function handlePlayerGlobalDownload(event:Event):void{
			_fbGlobalPlayerDir = createFolder(_flexHome + "\\frameworks\\libs\\player\\11.1");
			var playerGlobalFile:File = File.userDirectory.resolvePath(_fbGlobalPlayerDir.nativePath + "\\playerglobal.swc");
			var fs:FileStream = new FileStream();
			fs.open(playerGlobalFile, FileMode.WRITE);
			fs.writeBytes(event.target.data); 
			fs.close();
			
			copyConfigFiles();
		}
		
		protected function copyConfigFiles():void{
			log("Installing frameworks config files configured for use with Adobe Flash Builder");
			var configFilesDir:File = File.userDirectory.resolvePath(_flexHome + "\\ide\\flashbuilder\\config");
			var configFiles:Array = configFilesDir.getDirectoryListing();
			var flexHomeFrameworksDir:File = File.userDirectory.resolvePath(_flexHome + "\\frameworks");
			for each (var f:File in configFiles){
				if(isValidConfigFile(f)){
					var copyToFile:File = flexHomeFrameworksDir.resolvePath(f.name);
					f.copyTo(copyToFile,true);
				}
			}
			
			cleanup();
		}
		
		protected function cleanup():void{
			_flexTempDir.deleteDirectory(true);
			log("Installation complete.");
		}
		
		/************************ Utility methods for creating a folder, download a file, unzip a file *****************/
	
		protected function createFolder(path:String):File{
			var dir:File = new File(path);
			dir.createDirectory();
			return dir;
		}
		
		protected function download(url:String,handlerFunction:Function):void{
			var loader:URLLoader = new URLLoader(); 
			loader.dataFormat = URLLoaderDataFormat.BINARY; 
			loader.addEventListener(Event.COMPLETE, handlerFunction);
			loader.addEventListener(Event.COMPLETE,handleDownloadComplete);
			loader.addEventListener(ProgressEvent.PROGRESS,handleDownloadProgress);
			var req:URLRequest = new URLRequest(url); 
			loader.load(req);
		}
		
		private var _previousDisplayedPercent:int=0;
		protected function handleDownloadProgress(event:ProgressEvent):void{
			var bytesTotal:int = event.bytesTotal;
			var bytesLoaded:int = event.bytesLoaded;
			var percentLoaded:int = Math.round(bytesLoaded*100/bytesTotal);
			if(percentLoaded % 10 == 0 && percentLoaded != _previousDisplayedPercent){
				log("Downloaded " + percentLoaded + "%");
				_previousDisplayedPercent = percentLoaded;
			}
		}
		
		protected function handleDownloadComplete(event:Event):void{
			_previousDisplayedPercent = 0;
		}
		
		private function unzip(fileToUnzip:File, unzipCompleteFunction:Function):void{
			var zipFileBytes:ByteArray = new ByteArray();
			var fs:FileStream = new FileStream();
			fs.open(fileToUnzip, FileMode.READ);
			fs.readBytes(zipFileBytes);
			fs.close();
			var fzip:Zip = new Zip();
			fzip.addEventListener(ZipEvent.FILE_LOADED, onFileLoaded);
			fzip.addEventListener(Event.COMPLETE, unzipCompleteFunction);
			fzip.addEventListener(Event.COMPLETE, onUnzipComplete);
			fzip.loadBytes(zipFileBytes);
		}
		
		private function onFileLoaded(e:ZipEvent):void{
			var fzf:ZipFile = e.file;
			if (fzf.sizeUncompressed == 0 && fzf.sizeCompressed == 0){ //Is a directory, not a file. Dont try to write anything into it.    
				return;
			}
			var f:File = _flexHomeDir.resolvePath(fzf.filename);
			var fs:FileStream = new FileStream();
			fs.open(f, FileMode.WRITE);
			fs.writeBytes(fzf.content);
			fs.close();
		}
		
		private function onUnzipComplete(e:Event):void{
			var fzip:Zip = e.target as Zip;
			fzip.removeEventListener(ZipEvent.FILE_LOADED, onFileLoaded);
			fzip.removeEventListener(Event.COMPLETE, onUnzipComplete);
		}

		protected function isValidConfigFile(file:File):Boolean{
			var name:String = file.name;
			if(name.search("-config.xml") == -1){
				return false;
			}
			else{
				return true;
			}
		}

		
	]]>
</fx:Script>
	<s:TextArea id="_logTxtArea" top="0" bottom="0" left="0" right="0" contentBackgroundColor="0x000000" color="0xFFFFFF" />
</s:WindowedApplication>
